
plugins {
    id 'java'
    id 'application'
}

version = '0.0.1'
sourceCompatibility = 11
targetCompatibility = 11

repositories {
    jcenter()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.1',
          'org.junit.jupiter:junit-jupiter-params:5.5.1'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.1'
}

application {
    mainClassName = 'compiler.Compiler'
}

test {
    dependsOn 'cleanTest'
    useJUnitPlatform()
}

task sablecc(type: Exec) {
    doFirst {
        delete 'src/main/java/compiler/sc'
    }
    commandLine 'java',  '-jar', '.bin/sablecc.jar', 'src/main/java/grammaireL.sablecc'
}

task compileStuff(type: Exec) {
    workingDir 'src/test/resources/compare_arbres'
    commandLine 'make'
}

task evaluate(type: Exec) {
    dependsOn jar
    dependsOn compileStuff
    workingDir 'src/test/resources'
    commandLine './evaluate.py'
}

processResources {
    from ('src/main/java') {
        include '**/*.dat'
    }
}

jar {
    manifest {
        attributes('Main-Class': 'compiler.Compiler')
    }
}

clean {
    delete fileTree('src/test/resources/input').matching {
        include '**/*.ts', '**/*.sa', '**/*.sc', '**/*.c3a', '**/*.c3aout', '**/*.pre-nasm', '**/*.nasm',
                '**/*.fg', '**/*.fgs', '**/*.ig', '**/*.exe', '**/*.o', '**/*.out', '**/*.saout'
    }
}

compileJava.dependsOn sablecc
